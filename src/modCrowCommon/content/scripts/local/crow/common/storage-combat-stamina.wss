
class CROW_StorageCombatStamina {
  var regen_buff_counter: CROW_CombatStaminaRegenBuffCounter;

  // caches the stamina regeneration the player has before CROW modifications.
  // this is used to calculate the stamina regeneration from the buff stacks
  // outside the regen.ws effect, to edit the experience bar to display the
  // value.
  var cached_stamina_regen_points: float;

  var cached_difficulty_scaling: int;
  var cached_difficulty_scaling_inverted: int;

  private function initRegenBuffCounterIfNeeded() {
    this.updateDifficultyScaling();

    if (!this.regen_buff_counter) {
      this.regen_buff_counter = new CROW_CombatStaminaRegenBuffCounter in this;
    }
  }

  private function updateDifficultyScaling() {
    this.cached_difficulty_scaling = CROW_getDifficultyScaling();
    this.cached_difficulty_scaling_inverted = CROW_getDifficultyScalingInverted();
  }

  public function tick(delta: float, regen_point: float) {
    this.cached_stamina_regen_points = regen_point;

    if (this.regen_buff_counter) {
      this.regen_buff_counter.tick(delta);
    }
  }

  public function addRegenBuff(buff_count: int) {
    this.initRegenBuffCounterIfNeeded();

    while (buff_count > 0) {
      this.regen_buff_counter.regen_buffs.PushBack(
        (new CROW_CombatStaminaRegenBuff in this.regen_buff_counter)
          .init()
      );

      buff_count -= 1;
    }
  }

  public function getBuffCount(): int {
    this.initRegenBuffCounterIfNeeded();

    return this.regen_buff_counter.counter as int;
  }

  public function increaseActiveBuffsDuration(seconds: float) {
    if (!this.regen_buff_counter) {
      return;
    }

    this.regen_buff_counter.increaseActiveBuffsDuration(seconds);
  }

  public function getRegenerationPerStackPerSecond(regen_point: float): float {
    return ((regen_point * regen_point + 100) / 100)
         * (0.5 + this.cached_difficulty_scaling_inverted * 0.05)
         + (0.2 + this.cached_difficulty_scaling_inverted * 0.05);
  }
  
  public function getRegenerationPerSecond(regen_point: float): float {
    return regen_point
         // start by decreasing base passive regen a bit
         * this.getBasePassiveRegenerationDecrease()
         // then for each regen buff add a bonus passive regen
         + this.getBuffCount() * this.getRegenerationPerStackPerSecond(regen_point);
  }

  private function getBasePassiveRegenerationDecrease(): float {
    return 0.15
         + this.cached_difficulty_scaling_inverted * 0.05;
  }
}

class CROW_CombatStaminaRegenBuffCounter {
  var regen_buffs: array<CROW_CombatStaminaRegenBuff>;
  var is_running: bool;
  var counter: int;

  function shouldEnd(): bool {
    return this.counter <= 0;
  }

  function tick(delta: float) {
    var remaining_buff_count: int;
    var now: float = SUH_now();
    var i: int;

    this.is_running = true;

    i = this.regen_buffs.Size() - 1;
    while (i >= 0) {
      if (this.regen_buffs[i].hasExpired(now)) {
        this.regen_buffs.Erase(i);
      }
      else {
        remaining_buff_count += 1;
      }

      i -= 1;
    }

    this.counter = remaining_buff_count;
  }

  function increaseActiveBuffsDuration(seconds: float) {
    for buff: CROW_CombatStaminaRegenBuff in this.regen_buffs {
      buff.creation_date += seconds;
    }
  }

  function onComplete(): bool {
    this.is_running = false;

    return true;
  }
}

class CROW_CombatStaminaRegenBuff {
  var creation_date: float;

  public function init(): CROW_CombatStaminaRegenBuff {
    this.creation_date = theGame.GetEngineTimeAsSeconds();

    return this;
  }

  public function hasExpired(now_timestamp: float): bool {
    // expires after 10 seconds
    return now_timestamp - this.creation_date > 10;
  }
}
