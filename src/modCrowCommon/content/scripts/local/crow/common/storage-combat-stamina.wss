
class CROW_StorageCombatStamina {
  var regen_buff_counter: CROW_CombatStaminaRegenBuffCounter;

  private function initRegenBuffCounterIfNeeded() {
    if (!this.regen_buff_counter) {
      this.regen_buff_counter = new CROW_CombatStaminaRegenBuffCounter in this;
    }
  }

  public function addRegenBuff(buff_count: int) {
    this.initRegenBuffCounterIfNeeded();

    while (buff_count > 0) {
      this.regen_buff_counter.regen_buffs.PushBack(
        (new CROW_CombatStaminaRegenBuff in this.regen_buff_counter)
          .init()
      );

      buff_count -= 1;
    }

    if (!this.regen_buff_counter.is_running) {
      if (!SU_hasCooldownWithTitle(this.regen_buff_counter.title)) {
        SU_addCustomCooldown(this.regen_buff_counter);
      }
    }
  }

  public function getBuffCount(): int {
    this.initRegenBuffCounterIfNeeded();

    return this.regen_buff_counter.counter as int;
  }

  public function increaseActiveBuffsDuration(seconds: float) {
    if (!this.regen_buff_counter) {
      return;
    }

    this.regen_buff_counter.increaseActiveBuffsDuration(seconds);
  }
}

class CROW_CombatStaminaRegenBuffCounter extends SU_Cooldown {
  default title = "CROW_CombatStaminaRegenBuffCounter";
  default format = 1;
  default icon_name = "icons\inventory\other\stonemedallion.dds";

  var regen_buffs: array<CROW_CombatStaminaRegenBuff>;
  var is_running: bool;

  function shouldEnd(): bool {
    return this.counter <= 0;
  }

  function tick(delta: float, engine_time_as_seconds: float) {
    var remaining_buff_count: int;
    var i: int;

    this.is_running = true;

    i = this.regen_buffs.Size() - 1;
    while (i >= 0) {
      if (this.regen_buffs[i].hasExpired(engine_time_as_seconds)) {
        this.regen_buffs.Erase(i);
      }
      else {
        remaining_buff_count += 1;
      }

      i -= 1;
    }

    this.counter = remaining_buff_count;
  }

  function increaseActiveBuffsDuration(seconds: float) {
    for buff: CROW_CombatStaminaRegenBuff in this.regen_buffs {
      buff.creation_date += seconds;
    }
  }

  function onComplete(): bool {
    this.is_running = false;

    return true;
  }
}

class CROW_CombatStaminaRegenBuff {
  var creation_date: float;

  public function init(): CROW_CombatStaminaRegenBuff {
    this.creation_date = theGame.GetEngineTimeAsSeconds();

    return this;
  }

  public function hasExpired(now_timestamp: float): bool {
    // expires after 10 seconds
    return now_timestamp - this.creation_date > 10;
  }
}
